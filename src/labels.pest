alpha = { 'a'..'z' | 'A'..'Z' }
digit = { '0'..'9' }
alphanumeric = { alpha | digit }
whitespace = _{ " " | "\t" | "\n" }

dns_label_chars = { alphanumeric | "-" }
subdomain_chars = { dns_label_chars | "." }

dns_label = @{ alphanumeric ~ (
  &(dns_label_chars ~ dns_label_chars) ~ dns_label_chars
  | alphanumeric
){1, 62} }
dns_subdomain = @{ &(subdomain_chars{2,255} ~ !subdomain_chars) ~ dns_label ~ ("." ~ dns_label)* }

label_prefix = _{ dns_subdomain ~ "/" }
label_name_inner = { alphanumeric | "-" | "_" | "." }
label_part = { alphanumeric ~ (
  &(label_name_inner ~ label_name_inner) ~ label_name_inner
  | alphanumeric
)* }

label_key_name = @{ label_part }
label_key = { label_prefix? ~ &(label_name_inner{1,63} ~ !label_name_inner) ~ label_key_name }
label_value = @{ label_part }
label = { label_key ~ "=" ~ label_value? }

label_list = _{ label ~ (whitespace+ ~ label)* }
labels = _{ SOI ~ whitespace* ~ label_list ~ whitespace* ~ EOI}

label_whole = _{ SOI ~ label ~ EOI }
label_key_whole = _{ SOI ~ label_key ~ EOI }
label_keyprefix_whole = _{ SOI ~ dns_subdomain ~ EOI }
label_keyname_whole = _{ SOI ~ label_key_name ~ EOI }
label_value_whole = _{ SOI ~ label_value? ~ EOI }

label_colon_spec = { label_key ~ ":" ~ label_value? }
label_colon_whole = _{ SOI ~ label_colon_spec ~ EOI }

labels_colon_csv = _{ SOI ~ label_colon_spec ~ ("," ~ label_colon_spec)* ~ EOI }
labels_colon_wsv = _{ SOI ~ label_colon_spec ~ (whitespace+ ~ label_colon_spec)* ~ EOI }

labels_colon_either = _{ SOI ~ label_colon_spec ~
  ( &"," ~ ("," ~ label_colon_spec)+
   | &whitespace ~ (whitespace+ ~ label_colon_spec)+
   | &EOI )? ~ EOI }

annotation_value = @{ ANY+ }
annotation_whole = _{ SOI ~ label_key ~ (":" | "=") ~ annotation_value? ~ EOI }